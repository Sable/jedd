include "../../jl/parse/java12.cup"

package polyglot.ext.jedd.parse;

import polyglot.ext.jedd.types.*;
import polyglot.ext.jedd.ast.*;
import polyglot.ext.jl.parse.Name;

parser Grm extends polyglot.ext.jl.parse.Grm {:
    public final JeddTypeSystem ts;
    public final JeddNodeFactory nf;

    public Grm(Lexer l, TypeSystem t, NodeFactory n, ErrorQueue q) {
        super(l, t, n, q);
        ts = (JeddTypeSystem) t;
        nf = (JeddNodeFactory) n;
    }
:};

non terminal TypeNode attribute;
non terminal TypeNode colon_attribute_opt;
non terminal LinkedList attribute_tail;
non terminal BDDTypeNode relation_type;
non terminal Replace replace;
non terminal Relprod relprod;
non terminal TypeNode[] replacement;
non terminal LinkedList replacement_tail;
non terminal BDDLitPiece relation_literal_piece;
non terminal LinkedList relation_literal_tail;
non terminal TypeNode[] attribute_phys;
non terminal LinkedList attribute_phys_tail;
non terminal LinkedList attribute_list;
non terminal Expr relation_expr;
non terminal Expr relation_expr_or_relprod;

terminal Token ARROW;
terminal Token BDDFALSE;
terminal Token BDDTRUE;

start with goal;

extend type ::= relation_type:a 
        {: RESULT = a; :}
;

relation_type ::=
        LT:p attribute_phys:a attribute_phys_tail:b GT:e
        {:
            b.addFirst(a);
            RESULT = parser.nf.BDDTypeNode( parser.pos(p, e), b );
        :}
        |
        LT:p GT:e
        {:
            RESULT = parser.nf.BDDTypeNode( parser.pos(p, e), new LinkedList() );
        :}
        ;

attribute_phys ::=
        attribute:a colon_attribute_opt:b
        {: 
            TypeNode[] ar = { a, b };
            RESULT = ar;
        :}
        ;

attribute_phys_tail ::=
        COMMA attribute_phys:a attribute_phys_tail:b
        {:
            b.addFirst(a);
            RESULT = b;
        :}

        |

        {: RESULT = new LinkedList(); :}
        ;

attribute ::=
        class_or_interface_type:a
        {: RESULT = a; :}
    ;

colon_attribute_opt ::=
        COLON attribute:a
        {: RESULT = a; :}
        |
        ;


drop { unary_expression_not_plus_minus ::= postfix_expression ; }

relation_expr ::=
        replace:a
        {: RESULT = a; :}
        |
        postfix_expression:a
        {: RESULT = a; :}
        ;

extend unary_expression_not_plus_minus ::= relation_expr_or_relprod:a
        {: RESULT = a; :}
        ;

relation_expr_or_relprod ::=
        relation_expr:a
        {: RESULT = a; :}
        |
        relprod:a
        {: RESULT = a; :}
        ;

relprod ::=
        relation_expr_or_relprod:a LBRACE attribute_list:b RBRACE GT LT
        relation_expr:c LBRACE attribute_list:d RBRACE:e
        {: RESULT = parser.nf.Join( parser.pos(a, e), a, c, b, d ); :}
        |
        relation_expr_or_relprod:a LBRACE attribute_list:b RBRACE LT GT
        relation_expr:c LBRACE attribute_list:d RBRACE:e
        {: RESULT = parser.nf.Compose( parser.pos(a, e), a, c, b, d ); :}
        ;

replace ::=
        LPAREN:p replacement:a replacement_tail:b RPAREN
        relation_expr:c
        {:
            b.addFirst(a);
            RESULT = parser.nf.Replace( parser.pos(p, c), c, b );
        :}
        ;

replacement ::=
        attribute:a ARROW attribute:b
        {:
            TypeNode[] ret = { a, b };
            RESULT = ret;
        :}
        | attribute:a ARROW
        {:
            TypeNode[] ret = { a, null };
            RESULT = ret;
        :}
        ;

replacement_tail ::=
        COMMA replacement:a replacement_tail:b
        {:
            b.addFirst( a );
            RESULT = b;
        :}
        |

        {:
            RESULT = new LinkedList();
        :}
        ;

attribute_list ::= attribute:a attribute_tail:b 
            {: b.addFirst( a ); RESULT = b; :}
            |

            {: RESULT = new LinkedList(); :}
            ;



attribute_tail ::=
        COMMA attribute:a attribute_tail:b
        {: b.addFirst( a ); RESULT = b; :}
        |

        {: RESULT = new LinkedList(); :}
        ;

extend literal ::=
            NEW LBRACE:p relation_literal_piece:a relation_literal_tail:b RBRACE:e
            {: 
                b.addFirst(a);
                RESULT = parser.nf.BDDLit( parser.pos(p, e), b );
            :}
        |
            BDDTRUE:p
            {: RESULT = parser.nf.BDDTrueFalse( parser.pos(p, p), true ); :}
        |
            BDDFALSE:p
            {: RESULT = parser.nf.BDDTrueFalse( parser.pos(p, p), false ); :}
        ;

relation_literal_piece ::=
        expression:e ARROW attribute:d colon_attribute_opt:p
        {: RESULT = parser.nf.BDDLitPiece( parser.pos(e, p), e, d, p ); :}
        ;

relation_literal_tail ::=
        COMMA relation_literal_piece:a relation_literal_tail:b
        {: 
            b.addFirst( a );
            RESULT = b;
        :}
        |

        {:
            RESULT = new LinkedList();
        :}
        ;

extend class_declaration ::=
        modifiers_opt:a CLASS:n IDENTIFIER:b LPAREN class_type:domain RPAREN 
                super_opt:c interfaces_opt:d class_body:e
            {: RESULT = parser.nf.ClassDeclAttribute(parser.pos(n, e),
                a, b.getIdentifier(), c, d, e, domain, parser.ts ); :}
    |
        modifiers_opt:a CLASS:n IDENTIFIER:b LPAREN INTEGER_LITERAL:bits RPAREN 
                super_opt:c interfaces_opt:d class_body:e
            {: RESULT = parser.nf.ClassDeclDomain(parser.pos(n, e),
                a, b.getIdentifier(), c, d, e, 
                parser.nf.IntLit(parser.pos(bits, e), IntLit.INT,
                    bits.getValue().intValue()), parser.ts ); :}
    |
        modifiers_opt:a CLASS:n IDENTIFIER:b LPAREN RPAREN 
                super_opt:c interfaces_opt:d class_body:e
            {: RESULT = parser.nf.ClassDeclPhysDom(parser.pos(n, e),
                a, b.getIdentifier(), c, d, e, parser.ts ); :}
    ;


drop { primary_no_new_array ::= class_instance_creation_expression ; }

extend array_access ::= class_instance_creation_expression:a LBRACK expression:b RBRACK:e 
        {: RESULT = parser.nf.ArrayAccess(parser.pos(a, e), a, b); :}
;

extend explicit_constructor_invocation ::= class_instance_creation_expression:a DOT THIS:n LPAREN argument_list_opt:b RPAREN SEMICOLON:e 
        {: RESULT = parser.nf.ThisCall(parser.pos(a, e), a, b); :}
;

extend explicit_constructor_invocation ::= class_instance_creation_expression:a DOT SUPER:n LPAREN argument_list_opt:b RPAREN SEMICOLON:e
        {: RESULT = parser.nf.SuperCall(parser.pos(a, e), a, b); :}
;

extend class_instance_creation_expression ::= class_instance_creation_expression:a DOT NEW simple_name:b LPAREN argument_list_opt:c RPAREN:e
        {: RESULT = parser.nf.New(parser.pos(a, e), a, b.toType(), c); :}
;

extend class_instance_creation_expression ::= class_instance_creation_expression:a DOT NEW simple_name:b LPAREN argument_list_opt:c RPAREN class_body:d 
        {: RESULT = parser.nf.New(parser.pos(a, d), a, b.toType(), c, d); :}
;

extend field_access ::= class_instance_creation_expression:a DOT IDENTIFIER:b 
        {: RESULT = parser.nf.Field(parser.pos(a, b), a, b.getIdentifier()); :}
;

extend method_invocation ::= class_instance_creation_expression:a DOT IDENTIFIER:b LPAREN argument_list_opt:c RPAREN:e
        {: RESULT = parser.nf.Call(parser.pos(a, e), a, b.getIdentifier(), c); :}
;

extend unary_expression_not_plus_minus ::= class_instance_creation_expression:a
        {: RESULT = a; :}
;

extend cast_expression ::= LPAREN relation_type:a RPAREN 
        unary_expression_not_plus_minus:b
        {: RESULT = parser.nf.Cast(parser.pos(a), a, b); :}
;
